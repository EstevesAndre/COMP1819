options
{
    LOOKAHEAD=2;
    // DEBUG_PARSER=true;
    // MULTI=true;
}

PARSER_BEGIN(parser)
    public class parser
    {
        public static void main(String args[]) throws ParseException {
            parser p = new parser(System.in);
            SimpleNode root = p.Program();

            root.dump("");
        }
    }
PARSER_END(parser)

/* Caracters to be skipped */
SKIP :
{
    " " |
    "\t" |
    "\r" |
    "\n" |
    <"//" (~["\n","\r"])* ("\n"|"\r"|"\r\n")> |         /* Inline comments */
    <"/*" (~["*"])* "*" (~["/"] (~["*"])* "*")* "/">    /* Block comments */
}

/* Reserved words */
TOKEN :
{
    < CLASS : "class"> |
    < EXTENDS : "extends" > |
    < PUBLIC : "public"> |
    < STATIC : "static"> |
    < VOID : "void"> |
    < MAIN : "main"> |
    < STRING : "String"> |
    < RETURN : "return"> |
    < INT : "int"> |
    < BOOL : "boolean"> |
    < IF : "if"> |
    < ELSE : "else"> |
    < WHILE : "while"> |
    < LENGTH : "length"> |
    < T : "true"> |
    < F : "false"> |
    < THIS : "this"> |
    < NEW : "new">
}

/* Stuff :) */
TOKEN :
{
    < L_PAR : "("> | 
    < R_PAR : ")"> | 
    < L_BRACE : "{"> | 
    < R_BRACE : "}"> | 
    < L_BRACKET : "["> | 
    < R_BRACKET : "]"> | 
    < SEMICOLON : ";"> | 
    < COMMA : ","> | 
    < DOT : ".">
}

/* Operations */
TOKEN :
{
    < ASSIGN : "="> | 
    < AND : "&&"> | 
    < LESSTHAN : "<"> | 
    < SUM : "+"> | 
    < SUB : "-"> | 
    < MULT : "*"> | 
    < DIV : "/"> |
    < NOT : "!">
}

/* IntegerLiteral and Identifiers */
TOKEN :
{
    < INTEGER_LITERAL : <DECIMAL> | <OCTAL> | <HEXADECIMAL> | <BINARY> > |
    < #DECIMAL: ["1"-"9"](["0"-"9"])* > |
    < #OCTAL: "0"(["0"-"7"])* > |
    < #HEXADECIMAL: ("0x" | "0X")(["0"-"9", "A"-"F", "a"-"f"])+ > |
    < #BINARY: ("0b" | "0B")(["0", "1"])+ > |
    
    < IDENTIFIER : <LETTER> (<LETTER>|<DIGIT>)* > |
    < #LETTER : ["$", "_", "a"-"z", "A"-"Z"] > |
    < #DIGIT : ["0"-"9"] >
}

SimpleNode Program(): {}
{
    ClassDeclaration() <EOF> {return jjtThis;}
}

/**
 * Class declaration
 */
void ClassDeclaration(): {}
{
    <CLASS> <IDENTIFIER> (<EXTENDS> <IDENTIFIER>)? 
    <L_BRACE> 
        ( VarDeclaration() )*
        ( <PUBLIC> ( MainDeclaration() | MethodDeclaration() ) )* 
    <R_BRACE>
}

/**
 * Variable declaration
 */
void VarDeclaration() : {}
{
    Type() <IDENTIFIER> <SEMICOLON>
}

/**
 * Main function declaration
 */
void MainDeclaration() : {}
{
    <STATIC> <VOID> <MAIN> <L_PAR> <STRING> <L_BRACKET> <R_BRACKET> <IDENTIFIER> <R_PAR>
    <L_BRACE>
        ( VarDeclaration() )*
        ( Statement() )*
    <R_BRACE>
}   

/**
 * Method declaration
 */
void MethodDeclaration() : {}
{
    Type() <IDENTIFIER> <L_PAR> Args() <R_PAR>
    <L_BRACE>
        ( VarDeclaration() )*
        ( Statement() )*
        <RETURN> Expression() <SEMICOLON>
    <R_BRACE>
}

/**
 * Arguments
 */
void Args() : {}
{
    ( Type() <IDENTIFIER> ( <COMMA> Type() <IDENTIFIER> )* )?
}

/**
 * Types possible
 *      int[]
 *      boolean
 *      <identifier>
 */
void Type() : {}
{
    ( <INT> ( <L_BRACKET> <R_BRACKET> )? ) |
    <BOOL> |
    <IDENTIFIER>
}

/**
 * Statements
 *      If then else blocks
 *      Assign values
 */
void Statement() : {}
{
    ( <L_BRACE> Statement() <R_BRACE> ) |
    ( <IF> <L_PAR> Expression() <R_PAR> Statement() <ELSE> Statement() ) | 
    ( <WHILE> <L_PAR> Expression() <R_PAR> Statement() ) |
    ( Expression() <SEMICOLON> ) | 
    ( <IDENTIFIER> ( ( <ASSIGN> Expression() <SEMICOLON> ) |
                   ( <L_BRACKET> Expression() <R_BRACKET> <ASSIGN> Expression() <SEMICOLON> ) ) )
}

/**
 * Expressions    
 */
void Expression() : {}
{
    ( <INTEGER_LITERAL> Expression_Alt() )| 
    ( <T> Expression_Alt() ) |
    ( <F> Expression_Alt() ) |
    ( <IDENTIFIER> Expression_Alt() ) |
    ( <THIS> Expression_Alt() ) |
    ( <NEW> ( ( <INT> <L_BRACKET> Expression() <R_BRACKET> Expression_Alt() ) |
            ( <IDENTIFIER> <L_PAR> <R_PAR> Expression_Alt() ) ) ) |
    ( <NOT> Expression() Expression_Alt() ) |
    ( <L_PAR> Expression() <R_PAR> Expression_Alt() )
}

/**
 * Auxiliar expression - to prevent left side recursion
 */
void Expression_Alt() : {}
{
    ( Op()  Expression() Expression_Alt() ) |
    ( <L_BRACKET> Expression() <R_BRACKET> Expression_Alt() ) |
    ( <DOT> ( ( <LENGTH> Expression_Alt() ) |
            ( <IDENTIFIER> <L_PAR> ( Expression() ( <COMMA> Expression() )* )? <R_PAR> Expression_Alt() ) ) ) |
    {}
}

/**
 * Operations
 */
void Op() : {}
{
    <AND> | 
    <LESSTHAN> |
    <SUM> |
    <SUB> |
    <MULT> |
    <DIV>
}